import os, strutils, json, strformat
from utils import newLine

# Templates representing Nim constructs
const
  importDesc = "import $1"
  typeDesc = "$1* = ref object of $2"
  propDesc = "$1*: $2"
  caseStmtDesc = "case kind*: $1"
  caseOfDesc = "of $1: $2*: $3"
  caseElseDesc = "else: discard"

proc addLine(content: var string, line: string="", lineBreak: int=1) =
  if not line.isNilOrEmpty: content.add(line)
  content.add(newLine(count=lineBreak))

proc addImport(content: var string, modules: string) =
  content.addLine(importDesc % modules, lineBreak=2)

proc addTypes(content: var string, types: JsonNode) =
  content.addLine("type")
  for name, obj in types:
    content.addLine(indent(typeDesc % [name, obj["extendFrom"].str], count=2))
    case obj["type"].str:
      of "regular":
        for prop in obj["props"]:
          content.addLine(indent(propDesc % [prop["name"].str, prop["type"].str], count=4))
      of "case":
        content.addLine(indent(caseStmtDesc % obj["kind"].str, count=4))
        for prop in obj["props"]:
          content.addLine(indent(caseOfDesc % [prop["of"].str, prop["name"].str, prop["type"].str], count=6))
        content.addLine(indent(caseElseDesc, count=6))
    content.addLine()

proc stmtTypes(): JsonNode = 
  result = %* {
    "Stmt": {
    "type": "regular",
    "extendFrom": "RootObj",
    "props": [
      {"name": "hasError", "type": "bool"},
    ]
    },
    "Expression": {
      "type": "regular",
      "extendFrom": "Stmt",
      "props": [
        {"name": "expression", "type": "Expr"},
      ]
    }, 
    "Print": {
      "type": "regular",
      "extendFrom": "Stmt",
      "props": [
        {"name": "expression", "type": "Expr"},
      ]
    },           
  }

proc exprTypes(): JsonNode = 
  # Different types of expressions in lox language
  result = %* {
    "Expr": {
      "type": "regular",
      "extendFrom": "RootObj",
      "props": [
        {"name": "hasError", "type": "bool"},
      ]
    },
    "Binary": {
      "type": "regular",
      "extendFrom": "Expr",
      "props": [
        {"name": "left", "type": "Expr"},
        {"name": "operator", "type": "Token"},
        {"name": "right", "type": "Expr"}
      ]
    },
    "Grouping": {
      "type": "regular",
      "extendFrom": "Expr",
      "props": [{"name": "expression", "type": "Expr"}]
    },
    "Literal": {
      "type": "case",
      "extendFrom": "Expr",
      "kind": "LiteralKind",
      "props": [
        {"of": "litString", "name":"strVal", "type": "string"},
        {"of": "litNumber", "name":"floatVal", "type": "float"},
        {"of": "litBool", "name":"boolVal", "type": "bool"},
      ]
    },
    "Unary": {
      "type": "regular",
      "extendFrom": "Expr",
      "props": [
        {"name": "operator", "type": "Token"},
        {"name": "right", "type": "Expr"}
      ]
    }
  }

proc generateExprAst(outputDir: string) =
  # Core steps to generate `expr.nim`
  var content = ""
  content.addLine("# autogenerated via tools/astgen.nim", lineBreak=2)
  content.addImport("token, literalKind")
  content.addTypes(exprTypes())
  # Finally, write to file
  writeFile(outputDir / "expr.nim", content)

proc generateStmtAst(outputDir: string) =
  # Core steps to generate `stmt.nim`
  var content = ""
  content.addLine("# autogenerated via tools/astgen.nim", lineBreak=2)
  content.addImport("token, literalKind, expr")
  content.addTypes(stmtTypes())
  # Finally, write to file
  writeFile(outputDir / "stmt.nim", content)

when isMainModule:
  let outputDir = getCurrentDir() / "src"
  echo fmt"Output directory: {outputDir}"
  discard existsOrCreateDir(outputDir)
  generateExprAst(outputDir)
  generateStmtAst(outputDir)
  
